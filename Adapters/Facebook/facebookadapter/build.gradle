import groovy.json.JsonSlurper

apply plugin: 'com.android.library'

def versionsFile = file("../versions.json")
def versionsData = new JsonSlurper().parse(versionsFile)

ext {
    mediationsdkVersion = versionsData.mediationsdk
    adapterVersion = versionsData.adapter
    sdkVersion = versionsData.sdk
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

repositories {
    google()
}

android {
    compileSdkVersion 34
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 1
        versionName adapterVersion
        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
        buildConfigField "String", "VERSION_NAME", "\"${defaultConfig.versionName}\""
    }
    buildTypes {
        debug{
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude '**/res.values.values.xml'
    }
}

 dependencies {
     implementation "com.unity3d.ads-mediation:mediation-sdk:${mediationsdkVersion}"
     implementation "com.facebook.android:audience-network-sdk:${sdkVersion}"
     implementation 'com.facebook.infer.annotation:infer-annotation:0.18.0'
     implementation 'org.jetbrains:annotations:16.0.2'
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
    exclude 'res.values.values.xml'
}

task createAAR(type: Copy,group:'buildTasks') {

    dependsOn (":${project.name}:assembleRelease")
    from "build/outputs/aar"
    into '../ReleaseCandidates/'
    include '*.aar'
    rename { String fileName ->
        fileName.replace('release', adapterVersion)
    }
}


